TEMPLATE	= app
CONFIG		+= qt warn_on
DEFINES		+= QT_DLL QT_THREAD_SUPPORT
OBJECTS_DIR = obj
MOC_DIR		= moc
TRANSLATIONS = po/qterm_chs.ts po/qterm_cht.ts


# specially for Win32
win32:RC_FILE	= qterm.rc
win32:DEFINES += HAVE_PYTHON _NO_ARTS_COMPILED _NO_ESD_COMPILED
win32:INCLUDEPATH += c:/python24/include c:/openssl/include
win32:LIBS += c:/python24/libs/python24.lib c:/openssl/lib/vc/libeay32MD.lib

# specially for MacOSX 
mac {
	QMAKE_LFFLAGS_SONAME = -Wl,-install_name,@executable_path/../Frameworks/
	RC_FILE = qterm.icns
	# copy files to application bundle
	conf.path = qterm.app/Contents/MacOS/
	conf.files = qterm.cfg address.cfg credits
	script.path = qterm.app/Contents/MacOS/script
	script.files = script/*.py
	pic.path = qterm.app/Contents/MacOS/pic
	pic.files = pic/*.png
	cursor.path = qterm.app/Contents/MacOS/cursor
	cursor.files = cursor/mac_16x16/*.xpm
	po.path = qterm.app/Contents/MacOS/po
	po.files = po/*.qm
	INSTALLS = conf script pic cursor po
	DEFINES += HAVE_PYTHON _NO_ARTS_COMPILED _NO_ESD_COMPILED
	INCLUDEPATH += /usr/include/malloc /System/Library/Frameworks/Python.framework/Versions/Current/Headers
	LIBS += -lssl -lcrypto -framework Python
}

# specially for *NIX
unix:!mac{
	DEFINES += HAVE_PYTHON _NO_ARTS_COMPILED _NO_ESD_COMPILED
	INCLUDEPATH += /usr/include/python2.3 
	LIBS += -lpython2.3 -lssl
}

HEADERS		=aboutdialog.h \
		aboutdialogui.h \
		addrdialog.h \
		addrdialogui.h \
		articledialog.h \
		articledialogui.h \
		imageviewer.h \
		imageviewerui.h \
		keydialog.h \
		keydialogui.h \
		msgdialog.h \
		msgdialogui.h \
		osdmessage.h \
		overlayWidget.h \
		popupMessage.h \
		popwidget.h \
		prefdialog.h \
		prefdialogui.h \
		progressBar.h \
		qtermbbs.h \
		qtermbuffer.h \
		qtermcanvas.h \
		qtermconfig.h \
		qtermconvert.h \
		qtermdecode.h \
		qtermframe.h \
		qterm.h \
		qtermhttp.h \
		qtermimage.h \
		qtermiplocation.h \
		qtermparam.h \
		qtermpython.h \
		qtermscreen.h \
		qtermsocket.h \
		qtermsound.h \
		qtermtelnet.h \
		qtermtextline.h \
		qtermtimelabel.h \
		qtermtoolbutton.h \
		qtermwindow.h \
		qtermwndmgr.h \
		qtermzmodem.h \
		quickdialog.h \
		quickdialogui.h \
		schemadialog.h \
		schemadialogui.h \
		soundconf.h \
		soundconfui.h \
		statusBar.h \
		trayicon.h \
		zmodemdialog.h \
		zmodemdialogui.h \
		ssh/crc32.h	\
		ssh/types.h	\
		ssh/getput.h	\
		ssh/qtermsshauth.h	\
		ssh/qtermsshbuffer.h	\
		ssh/qtermsshcipher.h	\
		ssh/qtermsshconst.h	\
		ssh/qtermsshdes.h	\
		ssh/qtermsshhash.h	\
		ssh/qtermsshkex.h	\
		ssh/qtermsshlogin.h	\
		ssh/qtermsshloginui.h	\
		ssh/qtermsshmd5.h	\
		ssh/qtermsshpacket.h	\
		ssh/qtermsshrsa.h	\
		ssh/qtermsshsession.h	\
		ssh/qtermsshsocket.h

		
SOURCES		= aboutdialog.cpp \
		aboutdialogui.cpp \
		addrdialog.cpp \
		addrdialogui.cpp \
		articledialog.cpp \
		articledialogui.cpp \
		imageviewer.cpp \
		imageviewerui.cpp \
		keydialog.cpp \
		keydialogui.cpp \
		main.cpp \
		msgdialog.cpp \
		msgdialogui.cpp \
		osdmessage.cpp \
		overlayWidget.cpp \
		popupMessage.cpp \
		popwidget.cpp \
		prefdialog.cpp \
		prefdialogui.cpp \
		progressBar.cpp \
		qtermbbs.cpp \
		qtermbuffer.cpp \
		qtermcanvas.cpp \
		qtermconfig.cpp \
		qtermconvert.cpp \
		qtermdecode.cpp \
		qtermframe.cpp \
		qtermhttp.cpp \
		qtermimage.cpp \
		qtermiplocation.cpp \
		qtermparam.cpp \
		qtermpython.cpp \
		qtermscreen.cpp \
		qtermsocket.cpp \
		qtermsound.cpp \
		qtermtelnet.cpp \
		qtermtextline.cpp \
		qtermtimelabel.cpp \
		qtermtoolbutton.cpp \
		qtermwindow.cpp \
		qtermwndmgr.cpp \
		qtermzmodem.cpp \
		quickdialog.cpp \
		quickdialogui.cpp \
		schemadialog.cpp \
		schemadialogui.cpp \
		soundconf.cpp \
		soundconfui.cpp \
		statusBar.cpp \
		trayicon.cpp \
		zmodemdialog.cpp \
		zmodemdialogui.cpp \
		ssh/crc32.cpp	\
		ssh/qtermsshauth.cpp	\
		ssh/qtermsshbuffer.cpp	\
		ssh/qtermsshdes.cpp	\
		ssh/qtermsshkex.cpp	\
		ssh/qtermsshlogin.cpp	\
		ssh/qtermsshloginui.cpp	\
		ssh/qtermsshmd5.cpp	\
		ssh/qtermsshpacket.cpp	\
		ssh/qtermsshrsa.cpp	\
		ssh/qtermsshsession.cpp	\
		ssh/qtermsshsocket.cpp


win32:SOURCES += trayicon_win.cpp
unix:!mac{
	SOURCES += trayicon_x11.cpp
}
mac:SOURCES += trayicon_mac.cpp

TARGET		= qterm
